plugins {
    id 'com.android.library'
    id 'maven-publish'
}
android {
    namespace 'com.tencent.vasdolly.helper'
    compileSdk rootProject.compileSdkVersion

    defaultConfig {
        minSdk rootProject.minSdkVersion
        targetSdk rootProject.targetSdkVersion
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':reader')
}

// Android 库发布配置
afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'com.yibaoshan.vasdolly'
                artifactId = 'helper'
                version = rootProject.VERSION
                
                artifact bundleReleaseAar
                artifact androidSourcesJar
                
                pom {
                    withXml {
                        def dependenciesNode = asNode().appendNode('dependencies')
                        
                        def readerDependency = dependenciesNode.appendNode('dependency')
                        readerDependency.appendNode('groupId', 'com.yibaoshan.vasdolly')
                        readerDependency.appendNode('artifactId', 'reader')
                        readerDependency.appendNode('version', rootProject.VERSION)
                        readerDependency.appendNode('scope', 'runtime')
                    }
                }
            }
        }
    }
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}


